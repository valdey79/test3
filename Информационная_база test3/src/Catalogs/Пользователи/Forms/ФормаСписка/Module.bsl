
&НаСервереБезКонтекста
Функция КомандаПолучитьИнформациюНаСервере(ID)
	
	ИнформацияПоПользователю = "";
	
	АдресРесурса = СтрШаблон("/api/users/%1",ID);      
	
	//создать соединение
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL();
	Соединение = Новый HTTPСоединение("reqres.in",,,,,30,ЗащищенноеСоединение);   
	
	//создать запрос
	Запрос = Новый HTTPЗапрос(АдресРесурса);
	
	//отправить запрос серверу
	Попытка	
		Ответ = Соединение.ВызватьHTTPМетод("GET",Запрос);	
	Исключение
				
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось отправить запрос, причина:" + ОписаниеОшибки();
		Сообщение.Сообщить(); 
		
		Возврат "";
		
	КонецПопытки;
	
	//проверка кода состояния
	Если Ответ.КодСостояния = 200 Тогда
		
		//обработка ответа
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);  
		
		ДанныеСоответствие = ПрочитатьJSON(ЧтениеJSON,Истина);
		
		Для каждого КлючЗначение Из ДанныеСоответствие["data"] Цикл
			
			Если ЗначениеЗаполнено(ИнформацияПоПользователю) Тогда	
				ИнформацияПоПользователю = ИнформацияПоПользователю + Символы.ПС;		
			КонецЕсли;	
			
			ИнформацияПоПользователю = ИнформацияПоПользователю + КлючЗначение.Ключ + ": " + КлючЗначение.Значение;
			
		КонецЦикла;
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Не удалось выполнить запрос, код состояния: %1, ответ сервера: %2",
		Ответ.КодСостояния,Ответ.ПолучитьТелоКакСтроку());
		Сообщение.Сообщить(); 
		
		Возврат "";
		
	КонецЕсли;
	
	Возврат ИнформацияПоПользователю;
	
КонецФункции

&НаКлиенте
Процедура КомандаПолучитьИнформацию(Команда)
	
	ИнформацияПоПользователю = КомандаПолучитьИнформациюНаСервере(ID); 
	
	Если ЗначениеЗаполнено(ИнформацияПоПользователю) Тогда		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ИнформацияПоПользователю;
		Сообщение.Сообщить();	
	КонецЕсли;
	
КонецПроцедуры
